model {
  
  for(i in 1:I){         
  
    for(k in 1:num_treat[i]){
      y_ik[i,k] ~ dbin(p_ik[i,k],n_ik[i,k])
      p_ik[i,k] = exp(mu[i]+delta[i,k])/(1 + exp(mu[i] + delta[i,k]))
    }
  
    mu[i] ~ dnorm(m_mu,prec_mu)
    w[i,1] = 0
    delta[i,1] = 0

    for(k in 2:num_treat[i]){
      w[i,k] = (delta[i,k] - d_kt[t_mat[i,k],lts_ind[t_mat[i,k],i]] + d_kt[t_mat[i,1],lts_ind[t_mat[i,1],i]])
      sw[i,k] = sum(w[i,1:(k-1)])/(k-1)
      taud[i,k] = tau*2*(k-1)/k
    
      md[i,k] = d_kt[t_mat[i,k],lts_ind[t_mat[i,k],i]] - d_kt[t_mat[i,1],lts_ind[t_mat[i,1],i]] + sw[i,k]
      delta[i,k] ~ dnorm(md[i,k],taud[i,k])  
    }
    

  }
  
  for(t in 1:TS){
    d_kt[1,t] <- 0
  }
  
  for(k in 2:K){
    for(n in 1:tsize[k]){
      mean_kt[k,n] = (d[k] - height_k[k]) +  2*(height_k[k])/(1+exp(-scale_k[k]*(short_year_ki[k,n] - center_k[k])))
      draw[k,n] ~ dnorm(mean_kt[k,n], prec_psi)
      d_kt[k,n] = z_k[k]*draw[k,n] + (1-z_k[k])*d[k]
    }
    for(n in (tsize[k]+1):TS){
      d_kt[k,n] = 0 
    }
  }
  
  z_k[1] = 0
  for(k in 2:K){
    z_k[k] ~ dbern(pi)
  }
  
  d[1] <- 0                                           
  for(k in 2:K){                                      
    d[k] ~ dnorm(0,0.0001)                            
  }
  
  height_k[1] = 0
  scale_k[1] = 0
  center_k[1] = 0
  for(k in 2:K){
    height_k[k] ~ dgamma(1,1)
    scale_k[k] ~ dgamma(1,1)
    center_k[k] ~ dunif(0,T)
  }
  
  sd ~ dunif(0,5)                                    
  tau <- pow(sd,-2)                                   
  psi ~ dunif(0,5)
  prec_psi = pow(psi,-2)
  
  pi ~ dbeta(1,1)
}