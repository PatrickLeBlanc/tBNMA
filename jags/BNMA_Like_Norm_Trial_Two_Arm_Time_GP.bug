model {

for(i in 1:I){                                    
  prec[i] <- pow(se[i],-2)
  y[i] ~ dnorm(delta[i], prec[i])
  
  delta[i] ~ dnorm(md[i],tau)                         
  md[i] = d_kt[t2[i],lts_ind[t2[i],i]] - d_kt[t1[i],lts_ind[t1[i],i]]                      
}

for(t in 1:TS){
  d_kt[1,t] <- 0
}

for(k in 2:K){
  d_kt[k,1:tsize[k]] ~ dmnorm(Mean[1:tsize[k],k], Sigma_inv[1:tsize[k],1:tsize[k],k])
  for(t in (tsize[k]+1):TS){
    d_kt[k,t] = 0
  }
  Sigma_inv[1:tsize[k],1:tsize[k],k] <- inverse(Sigma[1:tsize[k],1:tsize[k],k])
  
  for(t in 1:tsize[k]){
    Mean[t,k] = d[k]
    
    Sigma[t,t,k] = pow(psi,2) + pow(phi[k],2)
    for(j in (t+1):tsize[k]){
      Sigma[t,j,k] = pow(phi[k],2)*exp(-rho[k]*pow(short_year_ki[k,t] - short_year_ki[k,j],2) )
      Sigma[j,t,k] = Sigma[t,j,k]
    }
  }
  
}

psi ~ dunif(0,5)
phi[1] = 0
rho[1] = 0
for(k in 2:K){
  phi[k] ~ dunif(0, 5)
  rho[k] ~ dunif(0.001, 1)
}


d[1] <- 0                                           
for(k in 2:K){                                      
  d[k] ~ dnorm(0,0.0001)                            
}

sd ~ dunif(0,5)                                    
tau <- pow(sd,-2)                                   
   
  
}